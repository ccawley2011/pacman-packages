# Maintainer: WinterMute <davem@devkitpro.org>
# Contributor: Cameron Cawley <ccawley2011@gmail.com>

pkgname=nds-libid3tag
pkgver=0.15.1b
pkgrel=1
pkgdesc='A library for reading and (eventually) writing ID3 tags, both ID3v1 and the various versions of ID3v2. (for Nintendo DS homebrew development)'
arch=('any')
url='https://www.underbit.com'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://sourceforge.net/projects/mad/files/libid3tag/0.15.1b/libid3tag-${pkgver}.tar.gz")
sha256sums=('63da4f6e7997278f8a3fef4c6a372d342f705051d1eeb6a46a86b03610e26151')
depends=('nds-zlib')
makedepends=('nds-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('nds-portlibs')

build() {
  cd libid3tag-$pkgver

  source /opt/devkitpro/ndsvars.sh

  rm -f Makefile.in aclocal.m4 config.guess config.h.in config.sub configure depcomp install-sh ltmain.sh missing mkinstalldirs msvc++/Makefile.in
  touch AUTHORS NEWS ChangeLog
  autoreconf -fi

  CPPFLAGS="${CFLAGS} ${CPPFLAGS}" ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libid3tag-$pkgver

  source /opt/devkitpro/ndsvars.sh

  make DESTDIR="$pkgdir" install
}
